;Hummingbird-2 Encryption Algorithm

;A = Pointer to key
;B = Pointer to IV
;C = Pointer to message
;PUSH = Length of message

;Initialization
Init:
    SET PUSH, J
    SET PUSH, X
    SET PUSH, Y
    SET PUSH, Z
    SET PUSH, A
    SET PUSH, I
    SET C, State
    
    SET [C], [B]
    SET [C+1], [B+1]
    SET [C+2], [B+2]
    SET [C+3], [B+3]
    SET [C+4], [B]
    SET [C+5], [B+1]
    SET [C+6], [B+2]
    SET [C+7], [B+3]
    
    SET B, A
    SET I, 0
    
    Init_loop
    SET A, [C]
    ADD A, I
    JSR EWD16
    SET J, A
    
    SET A, [C+1]
    ADD A, J
    JSR EWD16
    SET X, A
    
    SET A, [C+2]
    ADD A, X
    JSR EWD16
    SET Y, A
    
    SET A, [C+3]
    ADD A, Y
    JSR EWD16
    SET Z, A
    
    SET A, [C]
    ADD A, Z
    SHL A, 3
    BOR A, EX
    SET [C], A
    ADD C, 1
    
    SET A, [C]
    ADD A, J
    SHR A, 1
    BOR A, EX
    SET [C], A
    ADD C, 1
    
    SET A, [C]
    ADD A, X
    SHL A, 8
    BOR A, EX
    SET [C], A
    ADD C, 1
    
    SET A, [C]
    ADD A, Y
    SHL A, 1
    BOR A, EX
    SET [C], A
    ADD C, 1
    
    XOR [C+4], [C]
    XOR [C+5], [C+1]
    XOR [C+6], [C+2]
    XOR [C+7], [C+3]
    
    ADD I, 1
    IFL I, 4
        SET PC, Init_loop
    
    SET I, POP
    SET A, POP
    SET Z, POP
    SET Y, POP
    SET X, POP
    SET J, POP
    SET PC, POP

;Encrypt Permutation
EWD16:
    SET PUSH, I
    SET PUSH, B
    
    SET I, 0
    EWD16_loop:
        XOR A, [B]
        JSR EMix
        ADD I, 1
        ADD B, 1
        IFN I, 4
            SET PC, EWD16_loop
    
    SET B, POP
    SET I, POP
    SET PC, POP

;Encrypt Mixing Function
EMix:
    SET PUSH, B
    
    SET B, SBoxE
    JSR FuncS
    SET B, A
    SHL B, 6
    BOR B, EX
    XOR B, A
    SHL A, 10
    BOR A, EX
    XOR A, B
    
    SET B, POP
    SET PC, POP

;Decrypt Permutation
DWD16:
    SET PUSH, I
    
    SET I, 0
    ADD B, 3
    DWD16_loop:
        JSR DMix
        XOR A, [B]
        ADD I, 1
        SUB B, 1
        IFN I, 4
            SET PC, EWD16_loop
    
    SET I, POP
    SET PC, POP

;Decrypt Mixing Function
DMix:
    SET PUSH, B
    
    SET B, A
    SHR B, 6
    BOR B, EX
    XOR B, A
    SHR A, 10
    BOR A, EX
    XOR A, B
    
    SET B, SBoxD
    JSR FuncS
    
    SET B, POP
    SET PC, POP

;A = Word to SBox
;B = SBox to use
FuncS:
    SET PUSH, C
    SET PUSH, I
    SET PUSH, X
    SET PUSH, Y
    SET PUSH, Z
    
    SET C, A
    SET A, 0
    SET I, 0
    
    FuncS_loop:
    ADD I, 1
    SHL C, 4
    SET X, EX
    SHR X, 2
    SET Y, EX
    SHR Y, 14
    ADD X, B
    ADD Y, 1
    MUL Y, 4
    SET Z, [X]
    SHL Z, Y
    SET Z, EX
    AND Z, 0xf
    SHL A, 4
    BOR A, Z
    ADD B, 4
    IFL I, 4
        SET PC, FuncS_loop
    
    SET Z, POP
    SET Y, POP
    SET X, POP
    SET I, POP
    SET C, POP
    SET PC, POP

State:
DAT 0x0, 0x0, 0x0, 0x0
DAT 0x0, 0x0, 0x0, 0x0

SBoxE:
DAT 0x7ce9, 0x215f, 0xb6d0, 0x48a3
DAT 0x4a16, 0x8f7c, 0x30ed, 0x59b2
DAT 0x2fc1, 0x56ad, 0xe834, 0x0b97
DAT 0xf458, 0x9721, 0xa30e, 0x6cdb

SBoxD:
DAT 0xb54f, 0xc690, 0xd3e8, 0x1a27
DAT 0x92f8, 0x0c36, 0x4d1e, 0x7ba5
DAT 0xc30a, 0xb45f, 0x9e6d, 0x2781
DAT 0xa769, 0x12c5, 0x348f, 0xdeb0